plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'idea' // Add this
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

// Example of setting Lombok version, for scenarios where the current version is incompatible with Java version
ext {
	lombokVersion = '1.18.38'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly "org.projectlombok:lombok:$lombokVersion"
	testCompileOnly "org.projectlombok:lombok:$lombokVersion"
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor "org.projectlombok:lombok:$lombokVersion"
	testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// === Adding a test sourceSet for integrationTest :: Jvm Test Suite ===
// note: running after jar as an example of a test that requires the jar file to run
// https://docs.gradle.org/current/userguide/jvm_test_suite_plugin.html
testing {
	suites {
		test {
			useJUnitJupiter()
		}

		integrationTest(JvmTestSuite) {
			dependencies {
				implementation project()
			}

			configurations.integrationTestImplementation {
				extendsFrom(configurations.testImplementation)
			}

			targets {
				all {
					testTask.configure {
						shouldRunAfter(test)
						dependsOn jar
					}
				}
			}
		}
	}
}

check.dependsOn integrationTest

// === Adding a test sourceSet for integrationTest :: Source Sets ===
// doc: https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests
/*
sourceSets {
	integrationTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
//  shouldRunAfter jar
	dependsOn jar

	useJUnitPlatform()

	testLogging {
		events "passed"
	}
}

check.dependsOn integrationTest

idea {
	module {
		// This forces IntelliJ to treat the directory as a test root
		testSources.from(sourceSets.integrationTest.java.srcDirs)
	}
}
*/
